buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath 'ch.kerbtier.esdi-injector:esdi-injector:0.1.+'
  }
}


plugins {
  id 'java'
  id 'antlr'
  id 'eclipse'
  id 'maven'
  id 'jacoco'
}

apply plugin: 'kerbtier.esdi'
 


/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}


group = 'ch.kerbtier.webb'
version = getVersionName()


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  compile 'org.lesscss:lesscss:1.7.0.1.1'
  compile 'org.eclipse.jetty:jetty-server:9.3.2.v20150730'
  compile 'org.apache.lucene:lucene-core:5.3.0'
  compile 'org.apache.lucene:lucene-analyzers-common:5.3.0'
  compile 'org.apache.lucene:lucene-queryparser:5.3.0'
  compile 'commons-io:commons-io:2.4'
  compile 'com.github.jknack:handlebars:2.2.3'

  compile('com.yahoo.platform.yui:yuicompressor:2.4.8') {
    exclude module: 'js' 
  }
  compile('com.google.javascript:closure-compiler:r2388') {
    exclude group: 'org.apache.ant'
  }
  compile 'com.google.guava:guava:18.0'
  
  compile 'ch.kerbtier.lanthanum:lanthanum:0.1.+'
  compile 'ch.kerbtier.esdi:esdi:0.1.+'

  antlr 'org.antlr:antlr4:4.5.+'

  testCompile 'org.testng:testng:6.9.6'
}

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}


jar {
  baseName = 'webb'
  version =  version
  manifest {
    attributes 'Implementation-Title': 'webb', 'Implementation-Version': version
  }
}

generateGrammarSource {
  maxHeapSize = "64m"
  
  arguments += ["-package", "ch.kerbtier.webb.transform2d.parser", "-visitor", "-no-listener", "-long-messages"]
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}
